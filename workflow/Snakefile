from Cython.Build import cythonize
import pandas as pd
import numpy as np
import os

configfile: "workflow/config/config.yml"

wildcard_constraints:
    run=r"\d+",
    runs=r"\d+",


pyr_path = "pyr/s={n_samples} sp={pyr_spec_edge_prob}/run/pyr_{run}.csv"
cx_path = "cx/e={exact} p={parallel} l={max_length} s={n_samples}/run/cx_{run}.csv"


#dataset_path = "results/real/"
#epinions_path = dataset_path + "epinions/test/"


random_path = "results/synthetic/random/n={n_nodes} m={n_edges} r={neg_edge_prob} re={neg_edge_dist_exact} d={directed}/"
er_path = "results/synthetic/er/n={n_nodes} p={prob_p} r={neg_edge_prob} re={neg_edge_dist_exact} d={directed}/"
complete_path = "results/synthetic/complete/n={n_nodes} r={neg_edge_prob} re={neg_edge_dist_exact} d={directed}/"
sbm_path = "results/synthetic/sbm/exp={exp} re={neg_edge_dist_exact} d={directed}/"

def get_cython_cmd():
    if os.name == 'nt':
        return "python setup_balance.py build_ext --inplace && type NUL > {output}"
    else:
        # remove option '-a' if you don't need to debug / profile
        return "C_INCLUDE_PATH=$(python -c 'import numpy; print(numpy.get_include())') cythonize -i -a {input} && touch {output}"


rule all: 
    threads: 4
    input:
        "results/__cythonized__",

        "results/synthetic/er/n=10 p=0.5 r=0.5 re=True d=False/cx/e=True p=False l=10 s=1/cx_2_combined.csv",
        "results/synthetic/er/n=10 p=0.5 r=0.5 re=True d=True/cx/e=True p=False l=10 s=1/cx_2_combined.csv",
        "results/synthetic/er/n=10 p=0.5 r=0.5 re=True d=False/cx/e=False p=False l=10 s=20/cx_2_combined.csv",
        "results/synthetic/er/n=10 p=0.5 r=0.5 re=False d=True/cx/e=False p=False l=10 s=20/cx_2_combined.csv", 

        "results/synthetic/er/n=10 p=0.5 r=0.5 re=True d=False/pyr/s=1000 sp=None/pyr_2_combined.csv",
        "results/synthetic/er/n=10 p=0.5 r=0.5 re=True d=False/pyr/s=1000 sp=None/pyr_2_combined.csv",
        "results/synthetic/er/n=10 p=0.5 r=0.5 re=True d=False/pyr/s=1000 sp=None/pyr_2_combined.csv",
        "results/synthetic/er/n=10 p=0.5 r=0.5 re=False d=False/pyr/s=1000 sp=None/pyr_2_combined.csv",
        
        "results/synthetic/complete/n=20 r=0.5 re=True d=False/cx/e=False p=False l=10 s=20/cx_2_combined.csv",
        "results/synthetic/complete/n=20 r=0.5 re=True d=False/pyr/s=1000 sp=1/pyr_2_combined.csv",
        
        "results/synthetic/random/n=20 m=50 r=0.5 re=True d=False/cx/e=False p=False l=10 s=20/cx_0_combined.csv",
        "results/synthetic/random/n=20 m=50 r=0.5 re=True d=False/pyr/s=1000 sp=1/pyr_3_combined.csv", 

        "results/synthetic/random/n=20 m=50 r=0.5 re=True d=False/pyr/s=1000 sp=1/pyr_10_combined.csv",  

        "results/synthetic/random/n=20 m=50 r=0.5 re=True d=False/pyr/s=1000 sp=1/pyr_10_combined.csv", 

        expand("results/synthetic/sbm/exp={exp} re={re} d=False/{rest}", exp=range(0, len(config['sbm-exps'])), re=['True', 'False'], rest=['pyr/s=1000 sp=None/pyr_2_combined.csv', 'cx/e=False p=False l=10 s=20/cx_0_combined.csv']),

        "results/synthetic/sbm/exp=2 re=True d=False/cx/e=False p=False l=10 s=20/cx_0_combined.csv",


rule test_exp:
    input:
        "results/synthetic/er/n=11 p=0.5 r=0.9 re=False d=False/pyr/s=100 sp=None/pyr_0_combined.csv",
        #"results/synthetic/er/n=100 p=0.5 r=0.9 re=False d=False/cx/e=False p=False l=10 s=5/cx_0_combined.csv",
        #"results/synthetic/er/n=10 p=0.5 r=0.9 re=False d=False/cx/e=True p=False l=10 s=1/cx_0_combined.csv",
        #"results/synthetic/er/n=10 p=0.5 r=0.9 re=False d=False/cx/e=False p=False l=10 s=10/cx_0_combined.csv",
        "results/synthetic/er/n=11 p=0.5 r=0.9 re=False d=False/cx/e=True p=False l=10 s=1/cx_0_combined.csv",
        "results/synthetic/er/n=11 p=0.5 r=0.9 re=False d=False/cx/e=False p=False l=10 s=10/cx_0_combined.csv",

rule cythonize:
    # Cythonizes the required files.
    input:
        "src/py_raccoon/balance_sampling.pyx",
        "src/py_raccoon/balance_spanning_trees.pyx"
    output:
        # actual output filename sytem-dependent and not relevant -> __cythonized__ used to mark dependencies and detect changes by snakemake.
        "results/__cythonized__"
    shell:
        get_cython_cmd()

rule combine_exps:
    input: 
        lambda wildcards: expand("{{path}}/run/{{alg}}_{run}.csv", run=list(range(0, int(wildcards.runs) + 1))),
    output: 
        "{path}/{alg}_{runs}_combined.csv",
    run:
        df_data = pd.concat([pd.read_csv(file) for file in input])
        df_data.to_csv(output[0])

rule random_pyr:
    #threads: 1 
    input:
        "results/__cythonized__"
    output:
        random_path + pyr_path,
    params:
        kind = "random",
        alg = "pyr",
    script:
        "scripts/experiment.py"

rule random_cx:
    #threads: 1 
    input:
        "results/__cythonized__"
    output:
        random_path + cx_path,
    params:
        kind = "random",
        alg="cx",
    script:
        "scripts/experiment.py"

rule complete_pyr:
    #threads: 1 
    input:
        "results/__cythonized__"
    output:
        complete_path + pyr_path, 
    params:
        kind = "complete",
        alg = "pyr",
    script:
        "scripts/experiment.py"

rule complete_cx:
    #threads: 1 
    input:
        "results/__cythonized__"
    output:
        complete_path + cx_path, 
    params:
        kind = "complete",
        alg="cx",
    script:
        "scripts/experiment.py"

rule er_pyr:
    #threads: 1 
    input:
        "results/__cythonized__"
    output:
        er_path + pyr_path, 
    params:
        kind = "er",
        alg = "pyr"
    script:
        "scripts/experiment.py"

rule er_cx:
    #threads: 1 
    input:
        "results/__cythonized__"
    output:
        er_path + cx_path, 
    params:
        kind = "er",
        alg="cx",
    script:
        "scripts/experiment.py"
    
rule sbm_pyr:
    #threads: 1 
    input:
        "results/__cythonized__"
    output:
        sbm_path + pyr_path
    params:
        kind = "sbm",
        alg = "pyr",
        com_sizes = lambda wildcards: config['sbm-exps'][int(wildcards.exp)]['sizes'],
        edge_probs = lambda wildcards: config['sbm-exps'][int(wildcards.exp)]['p'],
        neg_edge_probs = lambda wildcards: config['sbm-exps'][int(wildcards.exp)]['r'],
    script:
        "scripts/experiment.py"

rule sbm_cx:
    #threads: 1 
    input:
        "results/__cythonized__"
    output:
        sbm_path + cx_path,
    params:
        kind = "sbm",
        alg="cx",
        com_sizes = lambda wildcards: config['sbm-exps'][int(wildcards.exp)]['sizes'],
        edge_probs = lambda wildcards: config['sbm-exps'][int(wildcards.exp)]['p'],
        neg_edge_probs = lambda wildcards: config['sbm-exps'][int(wildcards.exp)]['r'],
    script:
        "scripts/experiment.py"